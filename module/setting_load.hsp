
// デフォルト設定
#define global DefaultSetting_fontName  "游ゴシック"
#define global DefaultSetting_fontSize  14
#define global DefaultSetting_fontStyle 0

// JSONキー値（オートマッパーとか作れないかな...）
#define global SettingKey_fontName  "font"
#define global SettingKey_fontSize  "font_size"
#define global SettingKey_fontStyle "font_style"

// 設定項目をグローバルで使えるように登録（取得のみできるように関数にする）
#define global fontName _application_setting_fontName()
#define global fontSize _application_setting_fontSize()
#define global fontStyle _application_setting_fontStyle()

// アプリケーションの設定を読み込む
#module application_setting
#deffunc SettingLoad str fname

    // 読み込み
    notesel jsonText
    noteload fname
    noteunsel

    // JSON形式を開く
    jsonopen@ jsonPtr, jsonText
    jsongets@ _fontName, "font", jsonPtr
    jsongeti@ _fontSize, "font_size", jsonPtr
    jsongeti@ _fontStyle, "font_style", jsonPtr

    // JSONを閉じる
    jsonclose@

    // デフォルト設定の復元
    if(_fontName == ""):_fontName = DefaultSetting_fontName
    if(_fontSize == 0):_fontSize = DefaultSetting_fontSize
    if(_fontStyle == 0):_fontStyle = DefaultSetting_fontStyle

    // 設定をJSONに
    jsonnewobj@ jsonPtr, -1, ""
    jsonputs@ jsonPtr, "font", _fontName
    jsonputi@ jsonPtr, "font_size", _fontSize
    jsonputi@ jsonPtr, "font_style", _fontStyle
    jsonout@ jsonText, jsonPtr
    jsonclose@

    // 設定を保存
    jsonText = cnvatos(jsonText)
    notesel jsonText
    notesave fname
    noteunsel

    return

// ゲッター
#defcfunc _application_setting_fontName
    return _fontName

#defcfunc _application_setting_fontSize
    return _fontSize

#defcfunc _application_setting_fontStyle
    return _fontStyle

#global