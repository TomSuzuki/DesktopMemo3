// ------------------------------------------------------------
// タスクトレイのメニューを定義
// ------------------------------------------------------------

// タスクトレイのメニュー
// モジュール内に結合する

#enum MenuID_NewMemo = 1
#enum MenuID_GitHub
#enum MenuId_Exit
#enum MenuID_MemoInFron
#enum MenuID_Alignment
#enum MenuID_SelectAlignment
#enum MenuID_MemoHidden
#enum MenuID_BtnDisabled
#enum MenuID_Version
#enum MenuID_ReloadMemoMenu
#enum MenuID_SelectMemoBase

#enum MemoMenuID_SelectAlignment = 0
#enum MemoMenuID_SelectHidden
#enum MemoMenuID_SelectCopy
#enum MemoMenuID_SelectMax

// CreateTasktrayMenu ...メニューを配置します。
#deffunc CreateTasktrayMenu

  dim hMenu, 4

  CreatePopupMenu // 選択したメモを整列
  hMenu.1 = stat

  CreatePopupMenu // 選択したメモを非表示
  hMenu.2 = stat

  CreatePopupMenu // 選択したメモをコピー
  hMenu.3 = stat

  CreatePopupMenu
  hMenu.0 = stat
  AppendMenu hMenu.0, 0, MenuID_NewMemo, MENU_MESSAGE_NewMemo
  AppendMenu hMenu.0, $800, 0, ""
  AppendMenu hMenu.0, 0, MenuID_MemoInFron, MENU_MESSAGE_MemoInFron
  AppendMenu hMenu.0, 0, MenuID_Alignment, MENU_MESSAGE_Alignment
  AppendMenu hMenu.0, 0, MenuID_MemoHidden, MENU_MESSAGE_Hidden
  AppendMenu hMenu.0, 0, MenuID_BtnDisabled, MENU_MESSAGE_BtnDisabled
  AppendMenu hMenu.0, 0, MenuID_ReloadMemoMenu, MENU_MESSAGE_ReloadMemoMenu
  AppendMenu hMenu.0, $800, 0, ""
  AppendMenu hMenu.0, $10, hMenu.1, MENU_MESSAGE_SelectAlignment
  AppendMenu hMenu.0, $10, hMenu.2, MENU_MESSAGE_SelectHidden
  AppendMenu hMenu.0, $10, hMenu.3, MENU_MESSAGE_SelectCopy
  AppendMenu hMenu.0, $800, 0, ""
  AppendMenu hMenu.0, 0, MenuID_GitHub, MENU_MESSAGE_GitHub
  AppendMenu hMenu.0, 0, MenuID_Version, MENU_MESSAGE_Version
  AppendMenu hMenu.0, $800, 0, ""
  AppendMenu hMenu.0, 0, MenuId_Exit, MENU_MESSAGE_Exit

  // メニュー状態を更新
  UpdateMenuStatus

  return

// UpdateMenuStatus ...メニューのチェック状態等を更新します。
#deffunc UpdateMenuStatus

  // 非表示機能
  isMemoHidden = GetGlobalStatus(STATUS_HIDDN)
  CheckMenuItem  hMenu.0, MenuID_MemoHidden, isMemoHidden * $8
  EnableMenuItem hMenu.0, MenuID_NewMemo,    isMemoHidden * $1
  EnableMenuItem hMenu.0, MenuID_MemoInFron, isMemoHidden * $1
  EnableMenuItem hMenu.0, MenuID_Alignment,  isMemoHidden * $1
  EnableMenuItem hMenu.0, hMenu.1,           isMemoHidden * $1
  EnableMenuItem hMenu.0, hMenu.2,           isMemoHidden * $1

  // タイトルバー機能
  isBtnDisabled = GetGlobalStatus(STATUS_BTN_DISABLED)
  CheckMenuItem hMenu.0, MenuID_BtnDisabled, isBtnDisabled * $8

  // メモ単独非表示
  foreach memo
    wid = getWid(memo.cnt)
    selectMemoId = MenuID_SelectMemoBase + MemoMenuID_SelectMax * wid + MemoMenuID_SelectAlignment
    hiddenMemoId = MenuID_SelectMemoBase + MemoMenuID_SelectMax * wid + MemoMenuID_SelectHidden
    flg = getIsHidden(memo.cnt)
    EnableMenuItem hMenu.1, selectMemoId, flg * $1
    CheckMenuItem  hMenu.2, hiddenMemoId, (flg == 0) * $8
  loop

  return

// ReloadMemoMenu ...メモメニューを並び替えて再作成します。
#deffunc ReloadMemoMenu

  // メニューを削除する
  foreach memo
    wid = getWid(memo.cnt)
    DeleteMemoMenu wid
  loop

  // メニュー情報を取得する
  sdim memoTexts, 64, length(memo)
  foreach memo
    memoTexts.cnt = getText(memo.cnt)
  loop

  // 文字配列を並び替える
  sortstr memoTexts, 0

  // メニューを生成する
  foreach memo
    sortget n, cnt
    wid = getWid(memo.n)
    flg = getIsHidden(memo.n)
    txt = getMenuText(memo.n)
    AppendMemoMenu wid,txt, flg
  loop

  return

// AppendMemoMenu ...メモをメニューに登録します。
#deffunc AppendMemoMenu int _p1, str _p2, int _p3

  // タイトル文字を設定
  s = _p2 : if s == "" : s = MENU_MEMO_NO_TITLE

  // IDを生成
  menuId1 = MenuID_SelectMemoBase + MemoMenuID_SelectMax * _p1 + MemoMenuID_SelectAlignment
  menuId2 = MenuID_SelectMemoBase + MemoMenuID_SelectMax * _p1 + MemoMenuID_SelectHidden
  menuId3 = MenuID_SelectMemoBase + MemoMenuID_SelectMax * _p1 + MemoMenuID_SelectCopy

  // メニューを追加
  AppendMenu hMenu.1, 0, menuId1, s
  AppendMenu hMenu.2, 0, menuId2, s
  AppendMenu hMenu.3, 0, menuId3, s

  // 初期状態を設定
  EnableMenuItem  hMenu.1, menuId1, _p3 * $1
  CheckMenuItem   hMenu.2, menuId2, (_p3 == 0) * $8

  return

// RemoveMemoMenu ...メモをメニューから削除します。
#deffunc DeleteMemoMenu int _p1

  // IDを生成
  menuId1 = MenuID_SelectMemoBase + MemoMenuID_SelectMax * _p1 + MemoMenuID_SelectAlignment
  menuId2 = MenuID_SelectMemoBase + MemoMenuID_SelectMax * _p1 + MemoMenuID_SelectHidden
  menuId3 = MenuID_SelectMemoBase + MemoMenuID_SelectMax * _p1 + MemoMenuID_SelectCopy

  // メニューを削除
  DeleteMenu hMenu.1, menuId1, 0
  DeleteMenu hMenu.2, menuId2, 0
  DeleteMenu hMenu.3, menuId3, 0

  return

// ChangeMemoMenu ...メモメニューの内容を更新します。
#deffunc ChangeMemoMenu int _p1, str _p2

  // タイトル文字を設定
  s = _p2 : if s == "" : s = MENU_MEMO_NO_TITLE

  // IDを生成
  menuId1 = MenuID_SelectMemoBase + MemoMenuID_SelectMax * _p1 + MemoMenuID_SelectAlignment
  menuId2 = MenuID_SelectMemoBase + MemoMenuID_SelectMax * _p1 + MemoMenuID_SelectHidden
  menuId3 = MenuID_SelectMemoBase + MemoMenuID_SelectMax * _p1 + MemoMenuID_SelectCopy

  // メニュー内容を更新
  ModifyMenu hMenu.1, menuId1, 0, menuId1, s
  ModifyMenu hMenu.2, menuId2, 0, menuId2, s
  ModifyMenu hMenu.3, menuId3, 0, menuId3, s

  // メニュー状態を更新
  UpdateMenuStatus

  // メニューを更新
  DrawMenuBar hMenu.1
  DrawMenuBar hMenu.2
  DrawMenuBar hMenu.3

  return

// メニュー処理イベント
*onTrayIconEvent

  // メニュー表示
  if lparam == 0x0202 || lparam == 0x0205 {
    gsel 0
    SetForegroundWindow hwnd
    TrackPopupMenu hMenu.0, $100, ginfo(0), ginfo(1), 0, hwnd, 0
    if stat = 0:return

    // 各メニューの処理
    mstat = stat
    switch mstat

      // 新規メモの作成
      case MenuID_NewMemo
        createNewMemo ""
        swbreak
      
      // 終了
      case MenuId_Exit
        gosub *LabelExit@
        swbreak
      
      // GitHubのリンク
      case MenuID_GitHub
        exec appLink, 16
        swbreak
      
      // すべてのメモを非表示・表示
      case MenuID_MemoHidden

        // ステータスフラグを更新
        isMemoHidden = ToggleGlobalStatus(STATUS_HIDDN) // フラグを反転

        // メニュー状態を更新
        UpdateMenuStatus

        // メモの表示切替
        foreach memo
          MemoHidden memo.cnt, isMemoHidden
        loop
        swbreak
      
      // タイトルバーのボタンを非表示・表示
      case MenuID_BtnDisabled
        
        // ステータスフラグを更新
        isBtnDisabled = ToggleGlobalStatus(STATUS_BTN_DISABLED)

        // メニュー状態を更新
        UpdateMenuStatus

        // メモの表示を更新
        foreach memo
          Resize memo.cnt
        loop

        swbreak
      
      // 最前面に表示
      case MenuID_MemoInFron
        foreach memo
          InFront memo.cnt
        loop
        swbreak
      
      // 整列
      case MenuID_Alignment
        x = ButtonSize
        y = ButtonSize
        foreach memo
          if (getIsHidden(memo.cnt) == true) {
            continue
          }
          Alignment memo.cnt, x, y
          x += ButtonSize
          y += ButtonSize
        loop
        swbreak
      
      // バージョンダイアログを表示
      case MenuID_Version
        dialog DIALOG_MESSAGE_VERSION
        swbreak
      
      // 再読込み
      case MenuID_ReloadMemoMenu
        ReloadMemoMenu
        swbreak

    swend

    // メニュー以外（メモ）の処理
    foreach memo

      // IDを取得
      wid = getWid(memo.cnt)
      selectMemoId = MenuID_SelectMemoBase + MemoMenuID_SelectMax * wid + MemoMenuID_SelectAlignment
      hiddenMemoId = MenuID_SelectMemoBase + MemoMenuID_SelectMax * wid + MemoMenuID_SelectHidden
      copyMemoId = MenuID_SelectMemoBase + MemoMenuID_SelectMax * wid + MemoMenuID_SelectCopy

      // 整列
      if (mstat == selectMemoId) {
        Alignment memo.cnt, ButtonSize, ButtonSize
        InFront memo.cnt
        break
      }

      // 単独非表示
      if (mstat == hiddenMemoId) {
        SelectHidden memo.cnt
        UpdateMenuStatus
        break
      }

      // コピー
      if (mstat == copyMemoId) {
        text = getText(memo.cnt)
        clipset@ text
        break
      }

    loop
  }

  return
