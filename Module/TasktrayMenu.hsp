// ------------------------------------------------------------
// タスクトレイのメニューを定義
// ------------------------------------------------------------

// タスクトレイのメニュー
// モジュール内に結合する

#enum MenuID_NewMemo = 1
#enum MenuID_GitHub
#enum MenuId_Exit
#enum MenuID_MemoInFron
#enum MenuID_Alignment
#enum MenuID_SelectAlignment
#enum MenuID_MemoHidden
#enum MenuID_BtnDisabled
#enum MenuID_Version
#enum MenuID_Options
#enum MenuID_ReloadMemoMenu
#enum MenuID_SkipCopy
#enum MenuID_ShowScrollBar
#enum MenuID_SelectMemoBase

#enum MemoMenuID_SelectAlignment = 0
#enum MemoMenuID_SelectHidden
#enum MemoMenuID_SelectCopy
#enum MemoMenuID_SelectMax

#enum PopupMenu_Main = 0
#enum PopupMenu_Memo
#enum PopupMenu_Options
#enum PopupMenu_MemosBase

// CreateTasktrayMenu ...メニューを配置します。
#deffunc CreateTasktrayMenu

  dim hMenu, PopupMenu_MemosBase
  hMenuNextId = PopupMenu_MemosBase

  CreatePopupMenu // メモ
  hMenu.PopupMenu_Memo = stat

  CreatePopupMenu // オプションメモ
  hMenu.PopupMenu_Options = stat
  AppendMenu hMenu.PopupMenu_Options, 0, MenuID_ReloadMemoMenu, Localizer("menu_reload_memo_menu")
  AppendMenu hMenu.PopupMenu_Options, $800, 0, ""
  AppendMenu hMenu.PopupMenu_Options, 0, MenuID_MemoInFron, Localizer("menu_all_memo_in_front")
  AppendMenu hMenu.PopupMenu_Options, 0, MenuID_Alignment, Localizer("menu_all_memo_alignment")
  AppendMenu hMenu.PopupMenu_Options, $800, 0, ""
  AppendMenu hMenu.PopupMenu_Options, 0, MenuID_BtnDisabled, Localizer("menu_memo_btn_disabled")
  AppendMenu hMenu.PopupMenu_Options, 0, MenuID_ShowScrollBar, Localizer("menu_show_scroll_bar")
  AppendMenu hMenu.PopupMenu_Options, 0, MenuID_SkipCopy, Localizer("menu_skip_copy")

  CreatePopupMenu
  hMenu.PopupMenu_Main = stat
  AppendMenu hMenu.PopupMenu_Main, 0, MenuID_MemoHidden, Localizer("menu_all_memo_hidden")
  AppendMenu hMenu.PopupMenu_Main, $800, 0, ""
  AppendMenu hMenu.PopupMenu_Main, 0, MenuID_NewMemo, Localizer("menu_new_memo")
  AppendMenu hMenu.PopupMenu_Main, $10, hMenu.PopupMenu_Options, Localizer("menu_options")
  AppendMenu hMenu.PopupMenu_Main, $800, 0, ""
  AppendMenu hMenu.PopupMenu_Main, $10, hMenu.PopupMenu_Memo, Localizer("menu_memos")
  AppendMenu hMenu.PopupMenu_Main, $800, 0, ""
  AppendMenu hMenu.PopupMenu_Main, 0, MenuID_GitHub, Localizer("menu_github")
  AppendMenu hMenu.PopupMenu_Main, 0, MenuID_Version, Localizer("menu_version")
  AppendMenu hMenu.PopupMenu_Main, $800, 0, ""
  AppendMenu hMenu.PopupMenu_Main, 0, MenuId_Exit, Localizer("menu_exit")

  // メニュー状態を更新
  UpdateMenuStatus

  return

// UpdateMenuStatus ...メニューのチェック状態等を更新します。
#deffunc UpdateMenuStatus

  // 非表示機能
  isMemoHidden = GetGlobalStatus(STATUS_HIDDN)
  CheckMenuItem  hMenu.PopupMenu_Main,    MenuID_MemoHidden, isMemoHidden * $8
  EnableMenuItem hMenu.PopupMenu_Options, MenuID_NewMemo,    isMemoHidden * $1
  EnableMenuItem hMenu.PopupMenu_Options, MenuID_MemoInFron, isMemoHidden * $1
  EnableMenuItem hMenu.PopupMenu_Options, MenuID_Alignment,  isMemoHidden * $1

  // タイトルバー機能
  isBtnDisabled = GetGlobalStatus(STATUS_BTN_DISABLED)
  CheckMenuItem hMenu.PopupMenu_Options, MenuID_BtnDisabled, isBtnDisabled * $8

  // スキップコピー
  isSkipCopy = GetGlobalStatus(STATUS_SKIP_COPY)
  CheckMenuItem hMenu.PopupMenu_Options, MenuID_SkipCopy, isSkipCopy * $8
  skipCopyLabel = Localizer("menu_select_copy")
  if isSkipCopy : skipCopyLabel += Localizer("menu_select_copy_only_body") : else : skipCopyLabel += Localizer("menu_select_copy_all")

  // スクロールバーを表示
  isShowScrollbar = GetGlobalStatus(STATUS_SHOW_SCROLLBAR)
  CheckMenuItem hMenu.PopupMenu_Options, MenuID_ShowScrollBar, isShowScrollbar * $8

  // メモメニュー
  foreach memo
    hPopMenuId = getHMenu(memo.cnt)
    wid = getWid(memo.cnt)
    flg = getIsHidden(memo.cnt)

    selectMemoId = MenuID_SelectMemoBase + MemoMenuID_SelectMax * wid + MemoMenuID_SelectAlignment
    hiddenMemoId = MenuID_SelectMemoBase + MemoMenuID_SelectMax * wid + MemoMenuID_SelectHidden
    copyMemoId   = MenuID_SelectMemoBase + MemoMenuID_SelectMax * wid + MemoMenuID_SelectCopy

    EnableMenuItem hMenu.hPopMenuId, selectMemoId, flg * $1
    CheckMenuItem  hMenu.hPopMenuId, hiddenMemoId, (flg == 0) * $8
    ModifyMenu     hMenu.hPopMenuId, copyMemoId, 0, copyMemoId, skipCopyLabel
  loop

  // 描画を更新
  DrawMenuBar hMenu.PopupMenu_Main

  return

// ReloadMemoMenu ...メモメニューを並び替えて再作成します。
#deffunc ReloadMemoMenu

  // メニューを削除する
  foreach memo
    hPopMenuId = getHMenu(memo.cnt)
    DeleteMemoMenu hPopMenuId
  loop

  // メニュー情報を取得する
  sdim memoTexts, 64, length(memo)
  foreach memo
    memoTexts.cnt = getText(memo.cnt)
  loop

  // 文字配列を並び替える
  sortstr memoTexts, 0

  // メニューを生成する
  foreach memo
    sortget n, cnt
    wid = getWid(memo.cnt)
    hPopMenuId = getHMenu(memo.cnt)
    flg = getIsHidden(memo.n)
    txt = getMenuText(memo.n)
    _ = AppendMemoMenu(wid, txt, flg, hPopMenuId)
  loop

  return

// AppendMemoMenu ...メモをメニューに登録します。
#defcfunc AppendMemoMenu int _p1, str _p2, int _p3, int _p4

  // タイトル文字を設定
  s = _p2 : if s == "" : s = Localizer("menu_memo_no_title")

  // IDを生成
  menuId1 = MenuID_SelectMemoBase + MemoMenuID_SelectMax * _p1 + MemoMenuID_SelectAlignment
  menuId2 = MenuID_SelectMemoBase + MemoMenuID_SelectMax * _p1 + MemoMenuID_SelectHidden
  menuId3 = MenuID_SelectMemoBase + MemoMenuID_SelectMax * _p1 + MemoMenuID_SelectCopy

  // ポップメニューのIDを取得
  if (_p4 == -1) {
    hPopMenuId = hMenuNextId
    hMenuNextId = hMenuNextId + 1
  } else {
    hPopMenuId = _p4
  }

  // ポップメニューを追加
  CreatePopupMenu
  hMenu.hPopMenuId = stat

  // コピーメニューのラベル名を取得
  isSkipCopy = GetGlobalStatus(STATUS_SKIP_COPY)
  skipCopyLabel = Localizer("menu_select_copy")
  if isSkipCopy : skipCopyLabel += Localizer("menu_select_copy_only_body") : else : skipCopyLabel += Localizer("menu_select_copy_all")

  // メニューを追加
  AppendMenu hMenu.hPopMenuId, 0, menuId2, Localizer("menu_select_hidden")
  AppendMenu hMenu.hPopMenuId, 0, menuId1, Localizer("menu_select_alignment")
  AppendMenu hMenu.hPopMenuId, 0, menuId3, skipCopyLabel

  // ポップメニューをメニューに追加
  AppendMenu hMenu.PopupMenu_Memo, $10, hMenu.hPopMenuId, s

  // 初期状態を設定
  EnableMenuItem  hMenu.hPopMenuId, menuId1, _p3 * $1
  CheckMenuItem   hMenu.hPopMenuId, menuId2, (_p3 == 0) * $8

  return hPopMenuId

// RemoveMemoMenu ...メモをメニューから削除します。
#deffunc DeleteMemoMenu int _p1

  // メニューを削除
  DeleteMenu hMenu.PopupMenu_Memo, hMenu._p1, 0

  return

// ChangeMemoMenu ...メモメニューの内容を更新します。
#deffunc ChangeMemoMenu int _p1, str _p2

  // タイトル文字を設定
  s = _p2 : if s == "" : s = MENU_MEMO_NO_TITLE

  // メニュー内容を更新
  ModifyMenu hMenu.PopupMenu_Main, hMenu._p1, 0, hMenu._p1, s

  // メニュー状態を更新
  UpdateMenuStatus

  // メニューを更新
  DrawMenuBar hMenu.PopupMenu_Main

  return

// メニュー処理イベント
*onTrayIconEvent

  // メニュー表示
  if lparam == 0x0202 || lparam == 0x0205 {
    gsel 0
    SetForegroundWindow hwnd
    TrackPopupMenu hMenu.PopupMenu_Main, $100, ginfo(0), ginfo(1), 0, hwnd, 0
    if stat = 0:return

    // 各メニューの処理
    mstat = stat
    switch mstat

      // 新規メモの作成
      case MenuID_NewMemo
        createNewMemo ""
        swbreak
      
      // 終了
      case MenuId_Exit
        gosub *LabelExit@
        swbreak
      
      // GitHubのリンク
      case MenuID_GitHub
        exec appLink, 16
        swbreak
      
      // すべてのメモを非表示・表示
      case MenuID_MemoHidden

        // ステータスフラグを更新
        isMemoHidden = ToggleGlobalStatus(STATUS_HIDDN) // フラグを反転

        // メニュー状態を更新
        UpdateMenuStatus

        // メモの表示切替
        foreach memo
          MemoHidden memo.cnt, isMemoHidden
        loop
        swbreak
      
      // タイトルバーのボタンを非表示・表示
      case MenuID_BtnDisabled
        
        // ステータスフラグを更新
        isBtnDisabled = ToggleGlobalStatus(STATUS_BTN_DISABLED)

        // メニュー状態を更新
        UpdateMenuStatus

        // メモの表示を更新
        foreach memo
          Resize memo.cnt
        loop

        swbreak
      
      // 最前面に表示
      case MenuID_MemoInFron
        foreach memo
          InFront memo.cnt
        loop
        swbreak
      
      // 整列
      case MenuID_Alignment
        x = ButtonSize
        y = ButtonSize
        foreach memo
          if (getIsHidden(memo.cnt) == true) {
            continue
          }
          Alignment memo.cnt, x, y
          x += ButtonSize
          y += ButtonSize
        loop
        swbreak
      
      // バージョンダイアログを表示
      case MenuID_Version
        dialog DIALOG_MESSAGE_VERSION
        swbreak
      
      // 再読込み
      case MenuID_ReloadMemoMenu
        ReloadMemoMenu
        swbreak
      
      // スキップコピー
      case MenuID_SkipCopy

        // フラグを更新
        _ = ToggleGlobalStatus(STATUS_SKIP_COPY)

        // メニュー状態を更新
        UpdateMenuStatus

        swbreak
      
      // スクロールバーを表示
      case MenuID_ShowScrollBar

        // フラグを更新
        _ = ToggleGlobalStatus(STATUS_SHOW_SCROLLBAR)

        // メニュー状態を更新
        UpdateMenuStatus

        // メモの表示を更新
        foreach memo
          Resize memo.cnt
        loop

        swbreak

    swend

    // メニュー以外（メモ）の処理
    foreach memo

      // IDを取得
      wid = getWid(memo.cnt)
      selectMemoId = MenuID_SelectMemoBase + MemoMenuID_SelectMax * wid + MemoMenuID_SelectAlignment
      hiddenMemoId = MenuID_SelectMemoBase + MemoMenuID_SelectMax * wid + MemoMenuID_SelectHidden
      copyMemoId = MenuID_SelectMemoBase + MemoMenuID_SelectMax * wid + MemoMenuID_SelectCopy

      // 整列
      if (mstat == selectMemoId) {
        Alignment memo.cnt, ButtonSize, ButtonSize
        InFront memo.cnt
        break
      }

      // 単独非表示
      if (mstat == hiddenMemoId) {
        SelectHidden memo.cnt
        UpdateMenuStatus
        break
      }

      // コピー
      if (mstat == copyMemoId) {
        isSkipCopy = GetGlobalStatus(STATUS_SKIP_COPY)
        if (isSkipCopy == false) {
          text = getText(memo.cnt)
        } else {
          text = getSkipText(memo.cnt)
        }
        clipset@ text
        break
      }

    loop
  }

  return
