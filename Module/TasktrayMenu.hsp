// ------------------------------------------------------------
// タスクトレイのメニューを定義
// ------------------------------------------------------------

// タスクトレイのメニュー
// モジュール内に結合する

#enum MenuID_NewMemo = 1
#enum MenuID_GitHub
#enum MenuId_Exit
#enum MenuID_MemoInFron
#enum MenuID_Alignment
#enum MenuID_MemoHidden
#enum MenuID_Version

// CreateTasktrayMenu ...メニューを配置します。
#deffunc CreateTasktrayMenu

  CreatePopupMenu
  hMenu.0 = stat
  AppendMenu hMenu.0, 0, MenuID_NewMemo, MENU_MESSAGE_NewMemo
  AppendMenu hMenu.0, $800, 0, ""
  AppendMenu hMenu.0, 0, MenuID_MemoInFron, MENU_MESSAGE_MemoInFron
  AppendMenu hMenu.0, 0, MenuID_Alignment, MENU_MESSAGE_Alignment
  AppendMenu hMenu.0, 0, MenuID_MemoHidden, MENU_MESSAGE_Hidden
  AppendMenu hMenu.0, $800, 0, ""
  AppendMenu hMenu.0, 0, MenuID_GitHub, MENU_MESSAGE_GitHub
  AppendMenu hMenu.0, 0, MenuID_Version, MENU_MESSAGE_Version
  AppendMenu hMenu.0, $800, 0, ""
  AppendMenu hMenu.0, 0, MenuId_Exit, MENU_MESSAGE_Exit

  CheckMenuItem hMenu.0, MenuID_MemoHidden, 0

  return

// メニュー処理イベント
*onTrayIconEvent

  // メニュー表示
  if lparam == 0x0202 || lparam == 0x0205 {
    gsel 0
    SetForegroundWindow hwnd
    TrackPopupMenu hMenu.0, $100, ginfo(0), ginfo(1), 0, hwnd, 0
    if stat = 0:return

    // 各メニューの処理
    switch stat

      // 新規メモの作成
      case MenuID_NewMemo
        createNewMemo ""
        swbreak
      
      // 終了
      case MenuId_Exit
        gosub *LabelExit@
        swbreak
      
      // GitHubのリンク
      case MenuID_GitHub
        exec appLink, 16
        swbreak
      
      // すべてのメモを非表示・表示
      case MenuID_MemoHidden

        // チェック
        isMemoHidden = (isMemoHidden == 0)
        CheckMenuItem hMenu.0, MenuID_MemoHidden, isMemoHidden * $8

        // 無効化
        EnableMenuItem hMenu.0, MenuID_NewMemo, isMemoHidden * $1
        EnableMenuItem hMenu.0, MenuID_MemoInFron, isMemoHidden * $1
        EnableMenuItem hMenu.0, MenuID_Alignment, isMemoHidden * $1

        // メモの表示切替
        foreach memo
          MemoHidden memo.cnt, isMemoHidden
        loop
        swbreak
      
      // 最前面に表示
      case MenuID_MemoInFron
        foreach memo
          InFront memo.cnt
        loop
        swbreak
      
      // 整列
      case MenuID_Alignment
        x = ButtonSize
        y = ButtonSize
        foreach memo
          Alignment memo.cnt, x, y
          x += ButtonSize
          y += ButtonSize
        loop
        swbreak
      
      // バージョンダイアログを表示
      case MenuID_Version
        dialog DIALOG_MESSAGE_VERSION
        swbreak

    swend
  }

  return
